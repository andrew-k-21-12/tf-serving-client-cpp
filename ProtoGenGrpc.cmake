# Checking availability of the gRPC plugin.
find_program(GRPC_CPP_PLUGIN grpc_cpp_plugin)

# Creates cpp sources from proto with gRPC plugin.
function(PROTOBUF_GENERATE_GRPC_CPP SRCS HDRS)

    # Checking args.
    if(NOT ARGN)
        message(SEND_ERROR "Error: PROTOBUF_GENERATE_GRPC_CPP() called without any proto files")
        return()
    endif()

    # This variable is common for all types of output.
    if(PROTOBUF_GENERATE_CPP_APPEND_PATH)
        # Create an include path for each file specified
        foreach(FIL ${ARGN})
            get_filename_component(ABS_FIL ${FIL} ABSOLUTE)
            get_filename_component(ABS_PATH ${ABS_FIL} PATH)
            list(FIND _protobuf_include_path ${ABS_PATH} _contains_already)
            if(${_contains_already} EQUAL -1)
                list(APPEND _protobuf_include_path -I ${ABS_PATH})
            endif()
        endforeach()
    else()
        set(_protobuf_include_path -I ${CMAKE_CURRENT_SOURCE_DIR})
    endif()

    # Iterating through provided folders with proto files.
    if(DEFINED PROTOBUF_IMPORT_DIRS)
        foreach(DIR ${Protobuf_IMPORT_DIRS})
            get_filename_component(ABS_PATH ${DIR} ABSOLUTE)
            list(FIND _protobuf_include_path ${ABS_PATH} _contains_already)
            if(${_contains_already} EQUAL -1)
                list(APPEND _protobuf_include_path -I ${ABS_PATH})
            endif()
        endforeach()
    endif()

    # To return path to generated files.
    set(${SRCS})
    set(${HDRS})

    # Launching sources generation for all proto files.
    foreach(FIL ${ARGN})
        get_filename_component(ABS_FIL ${FIL} ABSOLUTE)
        get_filename_component(FIL_WE ${FIL} NAME_WE)
        get_filename_component(DIR_FIL ${FIL} DIRECTORY)

        # Adding generated sources to the sources list.
        list(APPEND ${SRCS} "${CMAKE_CURRENT_BINARY_DIR}/${DIR_FIL}/${FIL_WE}.grpc.pb.cc")
        list(APPEND ${HDRS} "${CMAKE_CURRENT_BINARY_DIR}/${DIR_FIL}/${FIL_WE}.grpc.pb.h")

        # Firing sources generation command.
        add_custom_command(
                OUTPUT
                    "${CMAKE_CURRENT_BINARY_DIR}/${DIR_FIL}/${FIL_WE}.grpc.pb.cc"
                    "${CMAKE_CURRENT_BINARY_DIR}/${DIR_FIL}/${FIL_WE}.grpc.pb.h"
                COMMAND
                    ${Protobuf_PROTOC_EXECUTABLE}
                ARGS
                    --grpc_out=${CMAKE_CURRENT_BINARY_DIR}
                    --plugin=protoc-gen-grpc=${GRPC_CPP_PLUGIN}
                    ${_protobuf_include_path} ${ABS_FIL}
                DEPENDS
                    ${ABS_FIL} ${Protobuf_PROTOC_EXECUTABLE}
                COMMENT
                    "Running gRPC C++ protocol buffer compiler on ${FIL}"
                VERBATIM)
    endforeach()

    # Returning generated sources list.
    set_source_files_properties(${${SRCS}} ${${HDRS}} PROPERTIES GENERATED TRUE)
    set(${SRCS} ${${SRCS}} PARENT_SCOPE)
    set(${HDRS} ${${HDRS}} PARENT_SCOPE)
endfunction()
